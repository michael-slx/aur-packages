# Maintainer: Michael Schantl <floss at schantl dash lx dot at>
# Contributor: Evan Goldenberg <evangoldenberg@gmail.com>

pkgname='hbase'
pkgver=2.3.1
pkgrel=1
pkgdesc='HBase - the Hadoop database'
arch=('i686' 'x86_64')
url='https://hbase.apache.org'
license=('apache')
depends=('java-environment>=8')
backup=(
  "etc/${pkgname}/hadoop-metrics2-hbase.properties"
  "etc/${pkgname}/hbase-env.sh"
  "etc/${pkgname}/hbase-policy.xml"
  "etc/${pkgname}/hbase-site.xml"
  "etc/${pkgname}/log4j.properties"
  "etc/${pkgname}/regionservers"
)
install="${pkgname}.install"
source=(
  "https://downloads.apache.org/hbase/$pkgver/hbase-$pkgver-bin.tar.gz"
  'hbase.install'
  'hbase.profile'
  'hbase.service'
  'hbase-site.xml'
)
sha512sums=('3192d22ed15dfc33bb41869fc14a3d4e6b141aff94b21366f63ca750d32b1eed985e491a952990c36ae6d9257b40b52e0829c3f4f5d9bfdfe04ab66293eea76f'
            '421d026111c356d0bec4b43cfc9d98a28cd182853037c228d59264f8758024771c52fe001195fe14785f7fe89d0d1d2ac37e9cd4418bd6a3d13d6b27d13d70c9'
            '43d421e2f1b0e8330f8564a25cea257f3e7e67afb6ad8ff47c6ebd0af9685c8564090b4f764a9d9d11dc2f88a454de3c2f9ed5c86638fd8c0a498bab3fae1cb5'
            'd91949bc5850d97f62a96551ae4904e6b4d591e044b84049773319383b256bf1465cb6a247cb830dd466bc8a1444853829e0a2336ca3d4815a993362a59a34a4'
            '0b6782087ee0eed822e919f171ef7ce10a3e95d9b2fa56122a37150690d14532c7824f736b1c8f65a22f9369a75ab5803d90736885b840b6571709a033d97ca2')
_watch="https://hbase.apache.org/downloads.html"

package() {
  cd "$srcdir/${pkgname}-${pkgver}"
  install -d \
    "$pkgdir/etc/hbase" \
    "$pkgdir/opt" \
    "$pkgdir/usr/bin" \
    "$pkgdir/usr/lib/systemd/system"
  cp -r "$srcdir/${pkgname}-${pkgver}" "$pkgdir/opt/hbase"

  cd "$pkgdir/usr/bin"
  for binary in hbase hbase-config.sh start-hbase.sh stop-hbase.sh; do
    ln -s "/opt/hbase/bin/$binary" $binary
  done

  install -Dm755 "$srcdir/hbase.profile" "$pkgdir/etc/profile.d/hbase.sh"
  install -Dm644 "$srcdir/hbase.service" "$pkgdir/usr/lib/systemd/system/hbase.service"
  install -Dm644 "$srcdir/${pkgname}-${pkgver}/conf"/* "$pkgdir/etc/hbase"
  install -Dm644 "$srcdir/hbase-site.xml" "$pkgdir/etc/hbase/hbase-site.xml"

  cd "$pkgdir/opt/hbase"
  mv conf conf-templates
  ln -sf "/etc/hbase" conf
}
