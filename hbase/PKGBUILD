# Maintainer: Michael Schantl <floss at schantl dash lx dot at>
# Contributor: Evan Goldenberg <evangoldenberg@gmail.com>

pkgname='hbase'
pkgver=2.3.0
pkgrel=2
pkgdesc='HBase - the Hadoop database'
arch=('i686' 'x86_64')
url='https://hbase.apache.org'
license=('apache')
depends=('java-environment>=8')
backup=(
  "etc/${pkgname}/hadoop-metrics2-hbase.properties"
  "etc/${pkgname}/hbase-env.cmd"
  "etc/${pkgname}/hbase-env.sh"
  "etc/${pkgname}/hbase-policy.xml"
  "etc/${pkgname}/hbase-site.xml"
  "etc/${pkgname}/log4j.properties"
  "etc/${pkgname}/regionservers"
)
install="${pkgname}.install"
source=(
  "https://downloads.apache.org/hbase/$pkgver/hbase-$pkgver-bin.tar.gz"
  'hbase.install'
  'hbase.profile'
  'hbase.service'
  'hbase-site.xml'
)
sha512sums=('5fb64fabfb6ead01527ece6e10be3d953681d09d6295883c966379f716c62e2fc289ae59609d7c1fe01296258e456d85516b7cfab0b962057a6713db6401a127'
            'b346cad56263965e49f5cffb020776a1749e40199aadd1ca47f0284f312acc49164e0513272221ac8152ca1624c986b5ebf977528129be8bcf668d31ad39f41e'
            '87be4f1d98ed33ff55117af09eed636ad99ca004666f4f0602e059ddc985e63466182465ab923052b2c9b474aea4f80f1ee39194642c39442c9b30d97727abd9'
            'd91949bc5850d97f62a96551ae4904e6b4d591e044b84049773319383b256bf1465cb6a247cb830dd466bc8a1444853829e0a2336ca3d4815a993362a59a34a4'
            '0b6782087ee0eed822e919f171ef7ce10a3e95d9b2fa56122a37150690d14532c7824f736b1c8f65a22f9369a75ab5803d90736885b840b6571709a033d97ca2')
_watch="https://hbase.apache.org/downloads.html"

package() {
  cd "$srcdir/${pkgname}-${pkgver}"
  install -d "$pkgdir/usr/bin" "$pkgdir/opt" "$pkgdir/etc/hbase" "$pkgdir/usr/lib/systemd/system"
  install -d "$pkgdir/var/log/hbase" "$pkgdir/var/lib/hbase" "$pkgdir/tmp/hbase"
  cp -r "$srcdir/${pkgname}-${pkgver}" "$pkgdir/opt/hbase"

  cd "$pkgdir/usr/bin"
  for binary in hbase hbase-config.sh start-hbase.sh stop-hbase.sh; do
    ln -s "/opt/hbase/bin/$binary" $binary
  done

  install -Dm755 "$srcdir/hbase.profile" "$pkgdir/etc/profile.d/hbase.sh"
  install -Dm644 "$srcdir/hbase.service" "$pkgdir/usr/lib/systemd/system/hbase.service"
  install -Dm644 "$srcdir/${pkgname}-${pkgver}/conf"/* "$pkgdir/etc/hbase"
  install -Dm644 "$srcdir/hbase-site.xml" "$pkgdir/etc/hbase/hbase-site.xml"

  cd "$pkgdir/opt/hbase"
  mv conf conf-templates
  ln -sf "/etc/hbase" conf
}
