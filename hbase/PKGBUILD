# Maintainer: Michael Schantl <floss at schantl dash lx dot at>
# Contributor: Evan Goldenberg <evangoldenberg@gmail.com>

# Supports automatic update checking

pkgname='hbase'
_MAJOR=2
_MINOR=4
_PATCH=7
pkgver=$_MAJOR.$_MINOR.$_PATCH

function _dl_url {
  echo "http://archive.apache.org/dist/$pkgname/$1.$2.$3/$pkgname-$1.$2.$3-bin.tar.gz"
}

pkgrel=1
pkgdesc='HBase - the Hadoop database'
arch=('any')
url='https://hbase.apache.org'
license=('apache')
depends=('java-environment>=8' 'bash')
backup=(
  "etc/${pkgname}/hadoop-metrics2-hbase.properties"
  "etc/${pkgname}/hbase-env.sh"
  "etc/${pkgname}/hbase-policy.xml"
  "etc/${pkgname}/hbase-site.xml"
  "etc/${pkgname}/log4j.properties"
  "etc/${pkgname}/regionservers"
)
install="${pkgname}.install"
source=(
  "$(_dl_url $_MAJOR $_MINOR $_PATCH)"
  'hbase.install'
  'hbase.profile'
  'hbase.service'
  'hbase-site.xml'
)
sha512sums=('7cc51b40a266f657a53d9e1e79493490cf67e6538d0f30e4d4e26f1560eb3fd3e93d50874cce6b614e5a2692c9e57af836d32f1f4bf53733b5b92d35f45ce730'
            'SKIP'
            'SKIP'
            'SKIP'
            'SKIP')
_watch="https://hbase.apache.org/downloads.html"

package() {
  cd "$srcdir/${pkgname}-${pkgver}"
  install -d \
    "$pkgdir/etc/hbase" \
    "$pkgdir/opt" \
    "$pkgdir/usr/bin" \
    "$pkgdir/usr/lib/systemd/system"
  cp -r "$srcdir/${pkgname}-${pkgver}" "$pkgdir/opt/hbase"

  cd "$pkgdir/usr/bin"
  for binary in hbase hbase-config.sh start-hbase.sh stop-hbase.sh; do
    ln -s "/opt/hbase/bin/$binary" $binary
  done

  install -Dm755 "$srcdir/hbase.profile" "$pkgdir/etc/profile.d/hbase.sh"
  install -Dm644 "$srcdir/hbase.service" "$pkgdir/usr/lib/systemd/system/hbase.service"
  install -Dm644 "$srcdir/${pkgname}-${pkgver}/conf"/* "$pkgdir/etc/hbase"
  rm "$pkgdir/etc/hbase/hbase-env.cmd"
  install -Dm644 "$srcdir/hbase-site.xml" "$pkgdir/etc/hbase/hbase-site.xml"

  cd "$pkgdir/opt/hbase"
  mv conf conf-templates
  ln -sf "/etc/hbase" conf
}
