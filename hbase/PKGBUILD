# Maintainer: Michael Schantl <floss at schantl dash lx dot at>
# Contributor: Evan Goldenberg <evangoldenberg@gmail.com>

pkgname='hbase'
pkgver=2.4.1
pkgrel=1
pkgdesc='HBase - the Hadoop database'
arch=('any')
url='https://hbase.apache.org'
license=('apache')
depends=('java-environment>=8')
backup=(
  "etc/${pkgname}/hadoop-metrics2-hbase.properties"
  "etc/${pkgname}/hbase-env.sh"
  "etc/${pkgname}/hbase-policy.xml"
  "etc/${pkgname}/hbase-site.xml"
  "etc/${pkgname}/log4j.properties"
  "etc/${pkgname}/regionservers"
)
install="${pkgname}.install"
source=(
  "https://downloads.apache.org/hbase/$pkgver/hbase-$pkgver-bin.tar.gz"
  'hbase.install'
  'hbase.profile'
  'hbase.service'
  'hbase-site.xml'
)
sha512sums=('5afb643c2391461619516624168e042b42a66e25217a3319552264c6af522e3a21a5212bfcba759b7b976794648ef13ee7b5a415f33cdb89bba43d40162aa685'
            'SKIP'
            'SKIP'
            'SKIP'
            'SKIP')
_watch="https://hbase.apache.org/downloads.html"

package() {
  cd "$srcdir/${pkgname}-${pkgver}"
  install -d \
    "$pkgdir/etc/hbase" \
    "$pkgdir/opt" \
    "$pkgdir/usr/bin" \
    "$pkgdir/usr/lib/systemd/system"
  cp -r "$srcdir/${pkgname}-${pkgver}" "$pkgdir/opt/hbase"

  cd "$pkgdir/usr/bin"
  for binary in hbase hbase-config.sh start-hbase.sh stop-hbase.sh; do
    ln -s "/opt/hbase/bin/$binary" $binary
  done

  install -Dm755 "$srcdir/hbase.profile" "$pkgdir/etc/profile.d/hbase.sh"
  install -Dm644 "$srcdir/hbase.service" "$pkgdir/usr/lib/systemd/system/hbase.service"
  install -Dm644 "$srcdir/${pkgname}-${pkgver}/conf"/* "$pkgdir/etc/hbase"
  rm "$pkgdir/etc/hbase/hbase-env.cmd"
  install -Dm644 "$srcdir/hbase-site.xml" "$pkgdir/etc/hbase/hbase-site.xml"

  cd "$pkgdir/opt/hbase"
  mv conf conf-templates
  ln -sf "/etc/hbase" conf
}
